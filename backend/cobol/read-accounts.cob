000100 IDENTIFICATION DIVISION.
000200  PROGRAM-ID. readaccounts.
000300  AUTHOR. Marco Cella.
000400  Installation. Read all user bank accounts.
000500  Date-Written. 2025/05/21.
000600  Date-Compiled. 2025/05/21.
000700*
000800 ENVIRONMENT DIVISION.
000900  CONFIGURATION SECTION.
001000   Source-Computer. ALPINE-3-21.
001100   Object-Computer. ALPINE-3-21.
001200  INPUT-OUTPUT SECTION.
001300   FILE-CONTROL.
001400     SELECT DATAFILE ASSIGN TO "/opt/app/storage/accounts.idx"
001500       ORGANIZATION IS INDEXED
001600       ACCESS MODE IS DYNAMIC
001700       RECORD KEY IS IKEY
001800       FILE STATUS IS FILE-STATUS.
001900*
002000 DATA DIVISION.
002100  FILE SECTION.
002200   FD DATAFILE
002300     RECORD CONTAINS 100 CHARACTERS.
002400   01 DATAFILEFD.
002500     05 IKEY PIC 9(4).
002600     05 BALANCE-TOTAL PIC S9(29)V99 COMP-3.
002700     05 LAST-CREDIT-TRANSACTION PIC 9(4).
002800     05 LAST-DEBIT-TRANSACTION PIC 9(4).
002900*
003000  WORKING-STORAGE SECTION.
003100   01 FILE-STATUS PIC XX.
003200   01 WS-ENDOFFILE PIC 9 VALUE ZERO.
003300   01 WS-FILE-ERROR PIC 9 VALUE ZERO.
003400   01 WS-DATAFILEFD.
003500     05 WS-IKEY PIC 9(4).
003600     05 WS-BALANCE-TOTAL PIC S9(29)V99 COMP-3.
003700     05 WS-LAST-CREDIT-TRANSACTION PIC 9(4).
003800     05 WS-LAST-DEBIT-TRANSACTION PIC 9(4).
003900   77 AINDEX PIC 9(4).
004000   77 WS-START-IKEY PIC 9(4).
004100   01 MAX-ACCOUNTS-SIZE CONSTANT 100.
004200*
004300  LINKAGE SECTION.
004400   01 ACCOUNTS.
004500     05 USERACCOUNTS OCCURS MAX-ACCOUNTS-SIZE TIMES.
004600       10 ACCOUNTIKEY PIC 9(4).
004700       10 ACCOUNTBALANCETOTAL PIC S9(29)V99 COMP-3.
004800       10 LASTCREDITTRANSACTION PIC 9(4).
004900       10 LASTDEBITTRANSACTION PIC 9(4).
005000   77 RETURNCODE PIC XX.
005100*
005200 PROCEDURE DIVISION USING
005300  BY REFERENCE ACCOUNTS
005400  BY REFERENCE RETURNCODE.
005500     DISPLAY "READ ACCOUNTS."
005600*
005700     MOVE 0 TO WS-ENDOFFILE.
005800     MOVE 1 TO IKEY.
005900     MOVE 1 TO AINDEX.
006000     OPEN INPUT DATAFILE.
006100       IF FILE-STATUS = "00"
006200         DISPLAY "The file exists."
006300       ELSE IF FILE-STATUS = "35"
006400         DISPLAY "The file does not exist."
006500         MOVE FILE-STATUS TO RETURNCODE
006600         CLOSE DATAFILE
006700         GO TO QUIT
006800       ELSE
006900         DISPLAY "An error occurred. Status: " FILE-STATUS
007000         MOVE FILE-STATUS TO RETURNCODE
007100         CLOSE DATAFILE
007200         GO TO QUIT
007300       END-IF.
007400
007500       START DATAFILE KEY IS GREATER THAN OR EQUAL TO IKEY
007600         INVALID KEY
007700           DISPLAY "Key not found."
007800           MOVE "03" TO RETURNCODE
007900         NOT INVALID KEY
008000           PERFORM UNTIL WS-ENDOFFILE = 1
008100            READ DATAFILE NEXT RECORD INTO WS-DATAFILEFD
008200               AT END MOVE 1 TO WS-ENDOFFILE
008300               NOT AT END
008400                 IF AINDEX > MAX-ACCOUNTS-SIZE
008500                   MOVE 1 TO WS-ENDOFFILE
008600                 ELSE
008700                   MOVE WS-IKEY TO ACCOUNTIKEY (AINDEX)
008800                   MOVE WS-BALANCE-TOTAL TO 
008900                     ACCOUNTBALANCETOTAL (AINDEX)
009000                   MOVE WS-LAST-CREDIT-TRANSACTION 
009100                     TO LASTCREDITTRANSACTION (AINDEX)
009200                   MOVE WS-LAST-DEBIT-TRANSACTION
009300                     TO LASTDEBITTRANSACTION (AINDEX)
009400                   ADD 1 TO AINDEX
009500                 END-IF
009600             END-READ
009700           END-PERFORM
009800       END-START.
009900     CLOSE DATAFILE.
010000     MOVE "00" TO RETURNCODE
010100     GO TO QUIT.
010200  QUIT.
010300 EXIT PROGRAM.
