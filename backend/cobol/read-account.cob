000100 IDENTIFICATION DIVISION.
000200  PROGRAM-ID. readaccount.
000300  AUTHOR. Marco Cella.
000400  Installation. Read user bank account.
000500  Date-Written. 2025/05/21.
000600  Date-Compiled. 2025/05/21.
000700*
000800 ENVIRONMENT DIVISION.
000900  CONFIGURATION SECTION.
001000   Source-Computer. ALPINE-3-21.
001100   Object-Computer. ALPINE-3-21.
001200  INPUT-OUTPUT SECTION.
001300   FILE-CONTROL.
001400     SELECT DATAFILE ASSIGN TO "/opt/app/storage/accounts.idx"
001500       ORGANIZATION IS INDEXED
001600       ACCESS MODE IS DYNAMIC
001700       RECORD KEY IS IKEY
001800       FILE STATUS IS FILE-STATUS.
001900*
002000 DATA DIVISION.
002100  FILE SECTION.
002200   FD DATAFILE
002300     RECORD CONTAINS 100 CHARACTERS.
002400   01 DATAFILEFD.
002500     05 IKEY PIC 9(4).
002600     05 BALANCE-TOTAL PIC S9(29)V99 COMP-3.
002700     05 LAST-CREDIT-TRANSACTION PIC 9(4).
002800     05 LAST-DEBIT-TRANSACTION PIC 9(4).
002900*
003000  WORKING-STORAGE SECTION.
003100   01 FILE-STATUS PIC XX.
003200   01 WS-ENDOFFILE PIC 9 VALUE ZERO.
003300   01 WS-FILE-ERROR PIC 9 VALUE ZERO.
003400   01 WS-DATAFILEFD.
003500     05 WS-IKEY PIC 9(4).
003600     05 WS-BALANCE-TOTAL PIC S9(29)V99 COMP-3.
003700     05 WS-LAST-CREDIT-TRANSACTION PIC 9(4).
003800     05 WS-LAST-DEBIT-TRANSACTION PIC 9(4).
003900*
004000  LINKAGE SECTION.
004100   77 USERACCOUNTIKEY PIC 9(4).
004200   01 ACCOUNT.
004300     05 ACCOUNTIKEY PIC 9(4).
004400     05 ACCOUNTBALANCETOTAL PIC S9(29)V99 COMP-3.
004500     05 LASTCREDITTRANSACTION PIC 9(4).
004600     05 LASTDEBITTRANSACTION PIC 9(4).
004700   77 RETURNCODE PIC XX.
004800*
004900 PROCEDURE DIVISION USING
005000  BY REFERENCE USERACCOUNTIKEY
005100  BY REFERENCE ACCOUNT
005200  BY REFERENCE RETURNCODE.
005300     DISPLAY "READ ACCOUNT."
005400     DISPLAY "Key: " USERACCOUNTIKEY.
005500     IF USERACCOUNTIKEY IS NOT NUMERIC
005600       DISPLAY "Wrong key value."
005700       MOVE "01" TO RETURNCODE
005800       GO TO QUIT
005900     END-IF.
006000*
006100     MOVE USERACCOUNTIKEY TO IKEY.
006200*
006300     OPEN INPUT DATAFILE.
006400       IF FILE-STATUS = "00"
006500         DISPLAY "The file exists."
006600       ELSE IF FILE-STATUS = "35"
006700         DISPLAY "The file does not exist."
006800         MOVE FILE-STATUS TO RETURNCODE
006900         CLOSE DATAFILE
007000         GO TO QUIT
007100       ELSE
007200         DISPLAY "An error occurred. Status: " FILE-STATUS
007300         MOVE FILE-STATUS TO RETURNCODE
007400         CLOSE DATAFILE
007500         GO TO QUIT
007600       END-IF.
007700*
007800       READ DATAFILE INTO WS-DATAFILEFD
007900         KEY IS IKEY
008000           INVALID KEY
008100             DISPLAY "Key not found."
008200             MOVE "03" TO RETURNCODE
008300           NOT INVALID KEY
008400             MOVE WS-IKEY TO ACCOUNTIKEY
008500             MOVE WS-BALANCE-TOTAL TO ACCOUNTBALANCETOTAL
008600             MOVE WS-LAST-CREDIT-TRANSACTION 
008700               TO LASTCREDITTRANSACTION
008800             MOVE WS-LAST-DEBIT-TRANSACTION
008900               TO LASTDEBITTRANSACTION
009000             MOVE "00" TO RETURNCODE
009100       END-READ.
009200     CLOSE DATAFILE.
009300     GO TO QUIT.
009400  QUIT.
009500 EXIT PROGRAM.
