000100 IDENTIFICATION DIVISION.
000200  PROGRAM-ID. readdebits.
000300  AUTHOR. Marco Cella.
000400  Installation. Read the account debit transactions.
000500  Date-Written. 2025/05/21.
000600  Date-Compiled. 2025/05/21.
000700*
000800 ENVIRONMENT DIVISION.
000900  CONFIGURATION SECTION.
001000   Source-Computer. ALPINE-3-21.
001100   Object-Computer. ALPINE-3-21.
001200  INPUT-OUTPUT SECTION.
001300   FILE-CONTROL.
001400     SELECT DATAFILE ASSIGN TO "/opt/app/storage/transactions.idx"
001500       ORGANIZATION IS INDEXED
001600       ACCESS MODE IS DYNAMIC
001700       RECORD KEY IS IKEY
001800       ALTERNATE KEY IS SOURCE-ID WITH DUPLICATES
001900       ALTERNATE KEY IS DESTINATION-ID WITH DUPLICATES
002000       FILE STATUS IS FILE-STATUS.
002100*
002200 DATA DIVISION.
002300  FILE SECTION.
002400   FD DATAFILE
002500     RECORD CONTAINS 100 CHARACTERS.
002600   01 DATAFILEFD.
002700     05 IKEY PIC 9(4).
002800     05 SOURCE-ID PIC 9(4).
002900     05 DESTINATION-ID PIC 9(4).
003000     05 AMOUNT PIC S9(29)V99 COMP-3.
003100     05 DATEANDTIME.
003200       10 CURRENTDATE.
003300         15 DATE-YYYY PIC 9999.
003400         15 DATE-MM PIC 99.
003500         15 DATE-DD PIC 99.
003600       10 CURRENTTIME.
003700         15 TIME-HH PIC 99.
003800         15 TIME-MM PIC 99.
003900         15 TIME-SS PIC 99.
004000         15 TIME-MS PIC 999999.
004100*
004200  WORKING-STORAGE SECTION.
004300   01 FILE-STATUS PIC XX.
004400  
004500   01 WS-ENDOFFILE PIC 9 VALUE ZERO. 
004600   01 WS-DATAFILEFD.
004700     05 WS-IKEY PIC 9(4).
004800     05 WS-SOURCE-ID PIC 9(4).
004900     05 WS-DESTINATION-ID PIC 9(4).
005000     05 WS-AMOUNT PIC S9(29)V99 COMP-3.
005100     05 WS-DATEANDTIME.
005200       10 WS-CURRENTDATE.
005300         15 WS-DATE-YYYY PIC 9999.
005400         15 WS-DATE-MM PIC 99.
005500         15 WS-DATE-DD PIC 99.
005600       10 WS-CURRENTTIME.
005700         15 WS-TIME-HH PIC 99.
005800         15 WS-TIME-MM PIC 99.
005900         15 WS-TIME-SS PIC 99.
006000         15 WS-TIME-MS PIC 999999.
006100   77 ACCOUNT-IKEY PIC 9(4).
006200   77 TINDEX PIC 9(4).
006300   01 MAX-TRANSACTIONS-SIZE CONSTANT 10.
006400*
006500 LINKAGE SECTION.
006600   77 ACCOUNTIKEY PIC 9(4).
006700   77 STARTTRANSACTIONIKEY PIC 9(4).
006800   01 ACCOUNTTRANSACTIONS.
006900     05 TRANSACTIONS OCCURS MAX-TRANSACTIONS-SIZE TIMES.
007000       10 TRANSACTIONIKEY PIC 9(4).
007100       10 SOURCEID PIC 9(4).
007200       10 DESTINATIONID PIC 9(4).
007300       10 TRANSACTIONAMOUNT PIC S9(29)V99 COMP-3 VALUE ZERO.
007400       10 DATEYYYY PIC 9999.
007500       10 DATEMM PIC 99.
007600       10 DATEDD PIC 99.
007700       10 TIMEHH PIC 99.
007800       10 TIMEMM PIC 99.
007900       10 TIMESS PIC 99.
008000       10 TIMEMS PIC 999999.
008100   77 RETURNCODE PIC XX.
008200*
008300 PROCEDURE DIVISION USING
008400  BY REFERENCE ACCOUNTIKEY
008500  BY REFERENCE STARTTRANSACTIONIKEY
008600  BY REFERENCE ACCOUNTTRANSACTIONS
008700  BY REFERENCE RETURNCODE.
008800     DISPLAY "READ ACCOUNT TRANSACTIONS."
008900     DISPLAY "Key: " ACCOUNTIKEY.
009000     IF ACCOUNTIKEY IS NOT NUMERIC
009100       DISPLAY "Wrong key value."
009200       MOVE "01" TO RETURNCODE
009300       GO TO QUIT
009400     END-IF.
009500     IF STARTTRANSACTIONIKEY IS NOT NUMERIC
009600       DISPLAY "Wrong start transaction key value."
009700       MOVE "01" TO RETURNCODE
009800       GO TO QUIT
009900     END-IF.
010000*
010100     MOVE ACCOUNTIKEY TO ACCOUNT-IKEY.
010200*
010300     OPEN INPUT DATAFILE.
010400       IF FILE-STATUS = "00"
010500         DISPLAY "The file exists."
010600       ELSE IF FILE-STATUS = "35"
010700         DISPLAY "The file does not exist."
010800         MOVE FILE-STATUS TO RETURNCODE
010900         CLOSE DATAFILE
011000         GO TO QUIT
011100       ELSE
011200         DISPLAY "An error occurred. Status: " FILE-STATUS
011300         MOVE FILE-STATUS TO RETURNCODE
011400         CLOSE DATAFILE
011500         GO TO QUIT
011600       END-IF.
011700     CLOSE DATAFILE.
011800*
011900     DISPLAY "Start read file.".
012000     MOVE 1 TO TINDEX.
012100     MOVE ACCOUNT-IKEY TO SOURCE-ID.
012200     MOVE 0 TO WS-ENDOFFILE.
012300     OPEN INPUT DATAFILE.
012400       START DATAFILE KEY IS EQUAL TO SOURCE-ID
012500         INVALID KEY
012600           DISPLAY "Key not found."
012700           MOVE "03" TO RETURNCODE
012800         NOT INVALID KEY
012900           PERFORM UNTIL WS-ENDOFFILE = 1
013000             READ DATAFILE NEXT RECORD INTO WS-DATAFILEFD
013100               AT END MOVE 1 TO WS-ENDOFFILE
013200               NOT AT END
013300                 IF NOT WS-SOURCE-ID = ACCOUNT-IKEY 
013400                   OR TINDEX > MAX-TRANSACTIONS-SIZE
013500                   MOVE 1 TO WS-ENDOFFILE
013600                 ELSE
013700                   IF WS-IKEY > STARTTRANSACTIONIKEY
013800                     DISPLAY "Transaction key: " WS-IKEY
013900                     DISPLAY "Source ID: " WS-SOURCE-ID
014000                     DISPLAY "Destination ID: " WS-DESTINATION-ID
014100                     DISPLAY "Amount: " WS-AMOUNT
014200                     DISPLAY "Date: " WS-DATE-YYYY "/" WS-DATE-MM 
014300                       "/" WS-DATE-DD
014400                     DISPLAY "Time: " WS-TIME-HH ":" WS-TIME-MM 
014500                       ":" WS-TIME-SS "." WS-TIME-MS
014600                     DISPLAY "-----------------------------"
014700                     MOVE WS-IKEY TO TRANSACTIONIKEY (TINDEX)
014800                     MOVE WS-SOURCE-ID TO SOURCEID (TINDEX)
014900                     MOVE WS-DESTINATION-ID TO 
015000                       DESTINATIONID (TINDEX)
015100                     MOVE WS-AMOUNT TO TRANSACTIONAMOUNT (TINDEX)
015200                     MOVE WS-DATE-YYYY TO DATEYYYY (TINDEX)
015300                     MOVE WS-DATE-MM TO DATEMM (TINDEX)
015400                     MOVE WS-DATE-DD TO DATEDD (TINDEX)
015500                     MOVE WS-TIME-HH TO TIMEHH (TINDEX)
015600                     MOVE WS-TIME-MM TO TIMEMM (TINDEX)
015700                     MOVE WS-TIME-SS TO TIMESS (TINDEX)
015800                     MOVE WS-TIME-MS TO TIMEMS (TINDEX)
015900                     ADD 1 TO TINDEX
016000                   END-IF
016100                 END-IF
016200             END-READ
016300             MOVE "00" TO RETURNCODE
016400           END-PERFORM
016500       END-START.
016600     CLOSE DATAFILE.
016700     MOVE 0 TO WS-ENDOFFILE.
016800     DISPLAY "End read file.".
016900  QUIT.
017000 EXIT PROGRAM.
