000100 IDENTIFICATION DIVISION.
000200  PROGRAM-ID. createtransaction.
000300  AUTHOR. Marco Cella.
000400  Installation. Create transaction for one account.
000500  Date-Written. 2025/05/21.
000600  Date-Compiled. 2025/05/21.
000700*
000800 ENVIRONMENT DIVISION.
000900  CONFIGURATION SECTION.
001000   Source-Computer. ALPINE-3-21.
001100   Object-Computer. ALPINE-3-21.
001200  INPUT-OUTPUT SECTION.
001300   FILE-CONTROL.
001400     SELECT DATAFILE ASSIGN TO "/opt/app/storage/transactions.idx"
001500       ORGANIZATION IS INDEXED
001600       ACCESS MODE IS DYNAMIC
001700       RECORD KEY IS IKEY
001800       ALTERNATE KEY IS SOURCE-ID WITH DUPLICATES
001900       ALTERNATE KEY IS DESTINATION-ID WITH DUPLICATES
002000       FILE STATUS IS FILE-STATUS.
002100*
002200 DATA DIVISION.
002300  FILE SECTION.
002400   FD DATAFILE
002500     RECORD CONTAINS 100 CHARACTERS.
002600   01 DATAFILEFD.
002700     05 IKEY PIC 9(4).
002800     05 SOURCE-ID PIC 9(4).
002900     05 DESTINATION-ID PIC 9(4).
003000     05 AMOUNT PIC S9(29)V99 COMP-3.
003100     05 DATEANDTIME.
003200       10 CURRENTDATE.
003300         15 DATE-YYYY PIC 9999.
003400         15 DATE-MM PIC 99.
003500         15 DATE-DD PIC 99.
003600       10 CURRENTTIME.
003700         15 TIME-HH PIC 99.
003800         15 TIME-MM PIC 99.
003900         15 TIME-SS PIC 99.
004000         15 TIME-MS PIC 999999.
004100*
004200  WORKING-STORAGE SECTION.
004300   01 FILE-STATUS PIC XX.
004400   01 WS-FILE-ERROR PIC 9 VALUE ZERO.
004500   01 WS-DATAFILEFD.
004600     05 WS-IKEY PIC 9(4).
004700     05 WS-SOURCE-ID PIC 9(4).
004800     05 WS-DESTINATION-ID PIC 9(4).
004900     05 WS-AMOUNT PIC S9(29)V99 COMP-3.
005000     05 WS-DATEANDTIME.
005100       10 WS-CURRENTDATE.
005200         15 WS-DATE-YYYY PIC 9999.
005300         15 WS-DATE-MM PIC 99.
005400         15 WS-DATE-DD PIC 99.
005500       10 WS-CURRENTTIME.
005600         15 WS-TIME-HH PIC 99.
005700         15 WS-TIME-MM PIC 99.
005800         15 WS-TIME-SS PIC 99.
005900         15 WS-TIME-MS PIC 999999.
006000   77 DISPLAY-DATE PIC XXXX/XX/XX.
006100   77 DISPLAY-TIME PIC XXXX/XX/XXXXXX.
006200   01 WS-NEXT-KEY PIC 9(4) VALUE ZEROS.
006300*
006400 LINKAGE SECTION.
006500   77 ACCOUNTIKEY PIC 9(4).
006600   77 DESTINATIONIKEY PIC 9(4).
006700   77 AMOUNTVALUE PIC S9(29)V99 COMP-3.
006800   01 TRANSACTION.
006900     05 TRANSACTIONIKEY PIC 9(4).
007000     05 SOURCEID PIC 9(4).
007100     05 DESTINATIONID PIC 9(4).
007200     05 TRANSACTIONAMOUNT PIC S9(29)V99 COMP-3.
007300     05 DATEYYYY PIC 9999.
007400     05 DATEMM PIC 99.
007500     05 DATEDD PIC 99.
007600     05 TIMEHH PIC 99.
007700     05 TIMEMM PIC 99.
007800     05 TIMESS PIC 99.
007900     05 TIMEMS PIC 999999.
008000   77 RETURNCODE PIC XX.
008100*
008200 PROCEDURE DIVISION USING
008300  BY REFERENCE ACCOUNTIKEY
008400  BY REFERENCE DESTINATIONIKEY
008500  BY REFERENCE AMOUNTVALUE
008600  BY REFERENCE TRANSACTION
008700  BY REFERENCE RETURNCODE.
008800     DISPLAY "CREATE ACCOUNT TRANSACTION."
008900     DISPLAY "Account Key: " ACCOUNTIKEY.
009000     IF ACCOUNTIKEY IS NOT NUMERIC
009100       DISPLAY "Wrong key value."
009200       MOVE "01" TO RETURNCODE
009300       GO TO QUIT
009400     END-IF.
009500*
009600     IF DESTINATIONIKEY IS NOT NUMERIC
009700       DISPLAY "Wrong destination value."
009800       MOVE "01" TO RETURNCODE
009900       GO TO QUIT
010000     END-IF.
010100*
010200     IF AMOUNTVALUE IS NOT NUMERIC
010300       DISPLAY "Wrong amount value."
010400       MOVE "50" TO RETURNCODE
010500       GO TO QUIT
010600     END-IF.
010700*
010800     IF AMOUNTVALUE IS < 0
010900       DISPLAY "Wrong amount value."
011000       MOVE "50" TO RETURNCODE
011100       GO TO QUIT
011200     END-IF.
011300*
011400     OPEN I-O DATAFILE.
011500       IF FILE-STATUS = "35"
011600         DISPLAY "File does not exist. Creating it."
011700         OPEN OUTPUT DATAFILE
011800       ELSE
011900         DISPLAY "The file exists."
012000       END-IF.
012100     CLOSE DATAFILE.
012200*
012300     MOVE 0 TO WS-NEXT-KEY.
012400     OPEN INPUT DATAFILE.
012500       IF FILE-STATUS = "00"
012600         START DATAFILE LAST
012700           INVALID KEY
012800             DISPLAY "File is empty. Starting from key 0001."
012900             MOVE 1 TO WS-NEXT-KEY
013000           NOT INVALID KEY
013100             READ DATAFILE NEXT RECORD INTO WS-DATAFILEFD
013200               NOT AT END
013300                 MOVE IKEY TO WS-NEXT-KEY
013400             END-READ
013500         END-START
013600       ELSE
013700         DISPLAY "Error opening file. Status: " FILE-STATUS
013800         MOVE FILE-STATUS TO RETURNCODE
013900         CLOSE DATAFILE
014000         GO TO QUIT
014100       END-IF
014200     CLOSE DATAFILE.
014300     ADD 1 TO WS-NEXT-KEY GIVING WS-NEXT-KEY.
014400     DISPLAY "Transaction Key: " WS-NEXT-KEY.
014500*
014600     MOVE WS-NEXT-KEY TO WS-IKEY.
014700     MOVE ACCOUNTIKEY TO WS-SOURCE-ID.
014800     MOVE DESTINATIONIKEY TO WS-DESTINATION-ID.
014900     MOVE AMOUNTVALUE TO WS-AMOUNT.
015000     ACCEPT WS-CURRENTDATE FROM DATE yyyymmdd.
015100     ACCEPT WS-CURRENTTIME FROM MICROSECOND-TIME.
015200*
015300     OPEN I-O DATAFILE.
015400       MOVE WS-IKEY TO IKEY
015500       MOVE WS-SOURCE-ID TO SOURCE-ID
015600       MOVE WS-DESTINATION-ID TO DESTINATION-ID
015700       MOVE WS-AMOUNT TO AMOUNT
015800       MOVE WS-CURRENTDATE TO CURRENTDATE
015900       MOVE WS-CURRENTTIME TO CURRENTTIME
016000       DISPLAY "Transaction key: " IKEY
016100       DISPLAY "Source ID: " SOURCE-ID
016200       DISPLAY "Destination ID: " DESTINATION-ID
016300       DISPLAY "Amount: " AMOUNT
016400       DISPLAY "Date: " CURRENTDATE
016500       DISPLAY "Time: " CURRENTTIME
016600       WRITE DATAFILEFD
016700         INVALID KEY MOVE 1 TO WS-FILE-ERROR
016800         NOT INVALID KEY DISPLAY "Item Added."
016900       END-WRITE
017000     CLOSE DATAFILE.
017100*
017200     MOVE WS-IKEY TO TRANSACTIONIKEY.
017300     MOVE WS-SOURCE-ID TO SOURCEID.
017400     MOVE WS-DESTINATION-ID TO DESTINATIONID.
017500     MOVE WS-AMOUNT TO TRANSACTIONAMOUNT.
017600     MOVE WS-DATE-YYYY TO DATEYYYY.
017700     MOVE WS-DATE-MM TO DATEMM.
017800     MOVE WS-DATE-DD TO DATEDD.
017900     MOVE WS-TIME-HH TO TIMEHH.
018000     MOVE WS-TIME-MM TO TIMEMM.
018100     MOVE WS-TIME-SS TO TIMESS.
018200     MOVE WS-TIME-MS TO TIMEMS.
018300*
018400     IF WS-FILE-ERROR IS ZERO
018500       MOVE "00" TO RETURNCODE
018600       GO TO QUIT
018700     ELSE
018800       DISPLAY "Error: Record already exists."
018900       MOVE WS-FILE-ERROR TO RETURNCODE
019000     END-IF.
019100  QUIT.
019200 EXIT PROGRAM.
