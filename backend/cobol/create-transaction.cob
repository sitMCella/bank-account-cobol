000100 IDENTIFICATION DIVISION.
000200  PROGRAM-ID. createtransaction.
000300  AUTHOR. Marco Cella.
000400  Installation. Create transaction for one account.
000500  Date-Written. 2025/05/21.
000600  Date-Compiled. 2025/05/21.
000700*
000800 ENVIRONMENT DIVISION.
000900  CONFIGURATION SECTION.
001000   Source-Computer. ALPINE-3-21.
001100   Object-Computer. ALPINE-3-21.
001200  INPUT-OUTPUT SECTION.
001300   FILE-CONTROL.
001400     SELECT DATAFILE ASSIGN TO "/opt/app/storage/transactions.idx"
001500       ORGANIZATION IS INDEXED
001600       ACCESS MODE IS DYNAMIC
001700       RECORD KEY IS IKEY
001800       ALTERNATE KEY IS SOURCE-ID WITH DUPLICATES
001900       ALTERNATE KEY IS DESTINATION-ID WITH DUPLICATES
002000       FILE STATUS IS FILE-STATUS.
002100*
002200 DATA DIVISION.
002300  FILE SECTION.
002400   FD DATAFILE
002500     RECORD CONTAINS 100 CHARACTERS.
002600   01 DATAFILEFD.
002700     05 IKEY PIC 9(4).
002800     05 SOURCE-ID PIC 9(4).
002900     05 DESTINATION-ID PIC 9(4).
003000     05 AMOUNT PIC S9(29)V99 COMP-3.
003100     05 DATEANDTIME.
003200       10 CURRENTDATE.
003300         15 DATE-YYYY PIC 9999.
003400         15 DATE-MM PIC 99.
003500         15 DATE-DD PIC 99.
003600       10 CURRENTTIME.
003700         15 TIME-HH PIC 99.
003800         15 TIME-MM PIC 99.
003900         15 TIME-SS PIC 99.
004000         15 TIME-MS PIC 999999.
004100*
004200  WORKING-STORAGE SECTION.
004300   01 FILE-STATUS PIC XX.
004400   01 WS-FILE-ERROR PIC 9 VALUE ZERO.
004500   01 WS-DATAFILEFD.
004600     05 WS-IKEY PIC 9(4).
004700     05 WS-SOURCE-ID PIC 9(4).
004800     05 WS-DESTINATION-ID PIC 9(4).
004900     05 WS-AMOUNT PIC S9(29)V99 COMP-3.
005000     05 WS-DATEANDTIME.
005100       10 WS-CURRENTDATE.
005200         15 WS-DATE-YYYY PIC 9999.
005300         15 WS-DATE-MM PIC 99.
005400         15 WS-DATE-DD PIC 99.
005500       10 WS-CURRENTTIME.
005600         15 WS-TIME-HH PIC 99.
005700         15 WS-TIME-MM PIC 99.
005800         15 WS-TIME-SS PIC 99.
005900         15 WS-TIME-MS PIC 999999.
006000   77 DISPLAY-DATE PIC XXXX/XX/XX.
006100   77 DISPLAY-TIME PIC XXXX/XX/XXXXXX.
006200   01 WS-NEXT-KEY PIC 9(4) VALUE ZEROS.
006300*
006400 LINKAGE SECTION.
006500   77 ACCOUNTIKEY PIC 9(4).
006600   77 DESTINATIONIKEY PIC 9(4).
006700   77 AMOUNTVALUE PIC S9(29)V99 COMP-3.
006800   01 TRANSACTION.
006900     05 TRANSACTIONIKEY PIC 9(4).
007000     05 SOURCEID PIC 9(4).
007100     05 DESTINATIONID PIC 9(4).
007200     05 TRANSACTIONAMOUNT PIC S9(29)V99 COMP-3.
007300     05 DATEYYYY PIC 9999.
007400     05 DATEMM PIC 99.
007500     05 DATEDD PIC 99.
007600     05 TIMEHH PIC 99.
007700     05 TIMEMM PIC 99.
007800     05 TIMESS PIC 99.
007900     05 TIMEMS PIC 999999.
008000   77 RETURNCODE PIC XX.
008100*
008200 PROCEDURE DIVISION USING
008300  BY REFERENCE ACCOUNTIKEY
008400  BY REFERENCE DESTINATIONIKEY
008500  BY REFERENCE AMOUNTVALUE
008600  BY REFERENCE TRANSACTION
008700  BY REFERENCE RETURNCODE.
008800     DISPLAY "CREATE ACCOUNT TRANSACTION."
008900     DISPLAY "Account Key: " ACCOUNTIKEY.
009000     IF ACCOUNTIKEY IS NOT NUMERIC
009100       DISPLAY "Wrong key value."
009200       MOVE "01" TO RETURNCODE
009300       GO TO QUIT
009400     END-IF.
009500*
009600     IF DESTINATIONIKEY IS NOT NUMERIC
009700       DISPLAY "Wrong destination value."
009800       MOVE "01" TO RETURNCODE
009900       GO TO QUIT
010000     END-IF.
010100*
010200     IF AMOUNTVALUE IS NOT NUMERIC
010300       DISPLAY "Wrong amount value."
010400       MOVE "50" TO RETURNCODE
010500       GO TO QUIT
010600     END-IF.
010700*
010800     IF AMOUNTVALUE IS < 0
010900       DISPLAY "Wrong amount value."
011000       MOVE "50" TO RETURNCODE
011100       GO TO QUIT
011200     END-IF.
011300*
011400     OPEN I-O DATAFILE.
011500       IF FILE-STATUS = "35"
011600         DISPLAY "File does not exist. Creating it."
011700         OPEN OUTPUT DATAFILE
011800       ELSE
011900         DISPLAY "The file exists."
012000       END-IF.
012100     CLOSE DATAFILE.
012200*
012300     MOVE ZEROS TO WS-NEXT-KEY.
012400     OPEN INPUT DATAFILE.
012500       IF FILE-STATUS = "00"
012600         START DATAFILE LAST
012700           INVALID KEY
012800             DISPLAY "File is empty. Starting from key 0001."
012900             MOVE 1 TO WS-NEXT-KEY
013000           NOT INVALID KEY
013100             DISPLAY "Last Transaction Key: " IKEY
013200             ADD 1 TO IKEY GIVING WS-NEXT-KEY
013300         END-START
013400       ELSE
013500         DISPLAY "Error opening file. Status: " FILE-STATUS
013600         MOVE FILE-STATUS TO RETURNCODE
013700         GO TO QUIT
013800       END-IF
013900     CLOSE DATAFILE.
014000*
014100     MOVE WS-NEXT-KEY TO WS-IKEY.
014200     MOVE ACCOUNTIKEY TO WS-SOURCE-ID.
014300     MOVE DESTINATIONIKEY TO WS-DESTINATION-ID.
014400     MOVE AMOUNTVALUE TO WS-AMOUNT.
014500     ACCEPT WS-CURRENTDATE FROM DATE yyyymmdd.
014600     ACCEPT WS-CURRENTTIME FROM MICROSECOND-TIME.
014700*
014800     OPEN I-O DATAFILE.
014900       MOVE WS-IKEY TO IKEY
015000       MOVE WS-SOURCE-ID TO SOURCE-ID
015100       MOVE WS-DESTINATION-ID TO DESTINATION-ID
015200       MOVE WS-AMOUNT TO AMOUNT
015300       MOVE WS-CURRENTDATE TO CURRENTDATE
015400       MOVE WS-CURRENTTIME TO CURRENTTIME
015500       DISPLAY "Transaction key: " IKEY
015600       DISPLAY "Source ID: " SOURCE-ID
015700       DISPLAY "Destination ID: " DESTINATION-ID
015800       DISPLAY "Amount: " AMOUNT
015900       DISPLAY "Date: " CURRENTDATE
016000       DISPLAY "Time: " CURRENTTIME
016100       WRITE DATAFILEFD
016200         INVALID KEY MOVE 1 TO WS-FILE-ERROR
016300         NOT INVALID KEY DISPLAY "Item Added."
016400       END-WRITE
016500     CLOSE DATAFILE.
016600*
016700     MOVE WS-IKEY TO TRANSACTIONIKEY.
016800     MOVE WS-SOURCE-ID TO SOURCEID.
016900     MOVE WS-DESTINATION-ID TO DESTINATIONID.
017000     MOVE WS-AMOUNT TO TRANSACTIONAMOUNT.
017100     MOVE WS-DATE-YYYY TO DATEYYYY.
017200     MOVE WS-DATE-MM TO DATEMM.
017300     MOVE WS-DATE-DD TO DATEDD.
017400     MOVE WS-TIME-HH TO TIMEHH.
017500     MOVE WS-TIME-MM TO TIMEMM.
017600     MOVE WS-TIME-SS TO TIMESS.
017700     MOVE WS-TIME-MS TO TIMEMS.
017800*
017900     IF WS-FILE-ERROR IS ZERO
018000       MOVE "00" TO RETURNCODE
018100       GO TO QUIT
018200     ELSE
018300       DISPLAY "Error: Record already exists."
018400       MOVE WS-FILE-ERROR TO RETURNCODE
018500     END-IF.
018600  QUIT.
018700 EXIT PROGRAM.
